# Managed prefix list holding the source range used by the EC2 Instance Connect
# service (the browser "Connect" button). us-east-1 -> 18.206.107.24/29.
data "aws_ec2_managed_prefix_list" "ec2_instance_connect" {
  name = "com.amazonaws.${var.aws_region}.ec2-instance-connect"
}

resource "aws_security_group" "bastion" {
  name        = "bastion-sg"
  # NOTE: SG description is immutable in AWS - changing it forces a replacement
  # of a security group that live instances depend on. Leave it alone.
  description = "Allow SSH from admin IP"
  vpc_id      = aws_vpc.main.id

  # SSH from your workstation (terminal / ansible)
  ingress {
    description = "SSH from admin IP"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = [var.my_ip_cidr]
  }

  # SSH from the EC2 Instance Connect service so the console Connect tab works
  ingress {
    description     = "SSH from EC2 Instance Connect service"
    from_port       = 22
    to_port         = 22
    protocol        = "tcp"
    prefix_list_ids = [data.aws_ec2_managed_prefix_list.ec2_instance_connect.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "bastion-sg"
  }
}

resource "aws_security_group" "app" {
  name        = "app-sg"
  description = "Allow SSH from bastion, app ports from admin IP"
  vpc_id      = aws_vpc.main.id

  ingress {
    description     = "SSH from bastion"
    from_port       = 22
    to_port         = 22
    protocol        = "tcp"
    security_groups = [aws_security_group.bastion.id]
  }

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = [var.my_ip_cidr]
  }

  ingress {
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = [var.my_ip_cidr]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "app-sg"
  }
}
